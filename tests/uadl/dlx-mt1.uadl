//
// A very simple, in-order multi-threaded model.  Two fetch units feed a
// dual-issue in-order pipeline.  Once in the pipe, the instructions move in
// lock-step.
//
define (core PMT) {

  allow_unimpl_instrs = true;

  define (thread=Thread) {
    define (resources) {
      // instruction buffer (fetch stage)
      define (fetchunit Fetcher) {
        fetch_memory = L1i;
        entries = 22;
        entry_size = 2;
        fetch_size = (8, 4);
        can_cancel = true;
        branch_predictor = BP;
        max_fetch_count = 1;
        // We want to interleave fetch/issue between threads for fairness.
        interleaved = true;
      };
      
      // branch predictor
      define (branch_predictor BP) {
        algorithm = Counter;
        size = 8;
        counter_width = 2;
      }

      define (semaphore=dead_flag) {}

    }

  }

  define (resources) {
    // Forwarding path.  Will assume a width of 32.
    define (forwarding_path=FwdEX) { size = 6; }

    // This has a size of 4 so that we can store ea + data for load-with-update
    // instructions for two instructions.
    define (forwarding_path=FwdMM) { size = 6; }

    define (flag hid_stall) {}
    define (semaphore all_stall) {}

    // memory system
    define (memory Mem) {
      data_width = 32;

      define (port I){
        type = instr;
        addr_bandwidth = 2;
        data_bandwidth = 2;
        max_requests = 2;
      }
      define (port D){
        type = data;
        addr_bandwidth = 2;
        data_bandwidth = 2;
        max_requests = 2;
      }
    }

    // memory system
    define (cache L1i) {
      next_level_memory = Mem.I;
      write_mode = WriteThrough;
      data_width = 64;
      max_requests = 4;
      addr_bandwidth = 4;
      data_bandwidth = 4;
      taken_to_write_latency = 2;
      preemptive = false;
      write_to_next_level_request_latency = 1;
      read_hit_latency = 0;
      streaming_linefill = true; 
      // Number of cycles during which a cache linefill cannot be performed
      // after a cache linefill is initiated.
      linefill_lock_cycle = 1; 
      // The lock is delayed by an extra cycle to model the maintenance cycle
      // when the linefill is dumped into the cache array
      linefill_lock_holdoff_cycle = 2;
      linefill_lazy_writeback = true; 
      linefill_lock_on_valid = true;
      linefill_read_latency = 0;
      // If linefill is accompanied by eviction, the additional cycles during
      // which the cache cannot do a lookup
      evict_lock_cycle = 1;
      // Make sure that we generate code for a store-buffer enable predicate correctly.
      store_buffer_enable = func() { return true; };
    }

    define (cache L1d) {
      custom = (MyCache,"MyCache.h");
      next_level_memory = Mem.D;
      write_mode = WriteThrough;
      data_width = 64;
      // Two transactions may be pipelined within the cache.
      max_requests = 2;
      store_buffer_size = 4;
      taken_to_write_latency = 2;
      preemptive = false;
      write_to_next_level_request_latency = 1;
      read_hit_latency = 0;    
      streaming_linefill = true; 
      // Number of cycles during which a cache linefill cannot be performed
      // after a cache linefill is initiated.
      linefill_lock_cycle = 0; 
      // The lock is delayed by an extra cycle to model the maintenance cycle
      // when the linefill is dumped into the cache array
      linefill_lock_holdoff_cycle = 2;
      linefill_lazy_writeback = false; 
      linefill_lock_on_valid = true;
      linefill_read_latency = 0;
      // Note: This uses a custom cache where block_on_critical_word is set
      // directly.  So, we're purposefully not setting it here.
      //block_on_critical_word = serialized;
      // If linefill is accompanied by eviction, the additional cycles during
      // which the cache cannot do a lookup
      evict_lock_cycle = 1;
      consecutive_linefills = true;
    }

    // Can't be lock-step or else we'll get deadlock if two instructions try and
    // issue, but one can't acquire its resources due to the other.
    define (pipestage=mID) {
      size = 2;
      issue = true;
    };

    define (pipestage=(mEX, mMM, mWB)) {
      size = 2;
      scheduler = lock_step;
    };
    
  }

  define (mem_alias) {
    Mem      = L1d;
  }

  define (machine normal) {
    init_state = S_INIT;
    states = ( S_ID, S_IDe, S_EX, S_EXp, S_EXe, S_MM, S_MMp, S_MMe, S_WB, S_WBp, S_WBe );

    // Maps states to stages.
    define (mapping) {
      mID = (S_ID,S_IDe);
      mEX = (S_EX,S_EXp,S_EXe);
      mMM = (S_MM,S_MMp,S_MMe);
      mWB = (S_WB,S_WBp,S_WBe);
    };

    // Defines the state transition on an exception:
    //  Lhs:  exception state.
    //  Rhs:  states which might generate an exception.
    define (exception_mapping) {
      S_IDe = S_ID;
      S_EXe = S_EX;
      S_MMe = S_MM;
      S_WBe = S_WB;
   }
  }

  // Common exception state transitions for all instruction classes.
# define ExceptionStates              \
    S_IDe: { change_state(S_EXe); };  \
    S_EXe: { change_state(S_MMe); };  \
    S_MMe: { change_state(S_WBe); };  \
    S_WBe: { change_state(S_INIT); };

  bool issueCheck(InstrType &instr,unsigned position,unsigned attrs) {
    // Basic thread check:  If we're not the first instruction issued in t0, then hold off if
    // t1 has instructions.
    if (getChildId() == 0 && position && !top.t1.Issue.empty()) return false;

    // Then perform the usual checks for multi-issue.
    return (attrs) ? (mID.has_space() && !mID.has_attr(*this,instr,0,0,attrs)) : true;
  }

  // Size of load/store instruction grouping.
# define LoadStoreGroupSize 4
# define ThreadCount 2

  // Similar to above, but has some extra scheduling logic.  We attempt to group
  // loads and stores together for a single thread.
  bool loadStoreIssueCheck(InstrType &instr,unsigned position,unsigned attrs) 
  {
    // Is the other thread's instruction, if it exists, a load/store instruction?
    bool ols = (getChildId() == 0) ? 
      top.t1.Issue.has_attr(*this,instr,0,(load_instrs|store_instrs)) :
      top.t0.Issue.has_attr(*this,instr,0,(load_instrs|store_instrs));

    if (ols && !loadStoreProceed(getChildId(),LoadStoreGroupSize)) return false;

    // Then perform the usual checks for multi-issue.
    bool ok = (attrs) ? (mID.has_space() && !mID.has_attr(*this,instr,0,0,attrs)) : true;

    if (ok) {
      loadStoreIncr(LoadStoreGroupSize,ThreadCount);
    }

    return ok;
  }

  // Common macro for dual-isue check.  We must have space in decode and, if an
  // instruction exists in position 0, then it must not have the attributes
  // specified by 'attrs'.  The has_attr() method is safe to call even if no
  // instructions exist, so we don't have to make a check to see if anything
  // exists in position 0.
  //
  // attrs:  bit-or'd list of attributes to check, e.g. attr1|attr2 ...
# define IssueCheck(attrs) if (issueCheck(*this,position,attrs))

# define LoadStoreIssueCheck(attrs) if (loadStoreIssueCheck(*this,position,attrs))

  // Returns true if decode is empty.  Use this for instructions which can only
  // single-issue.
# define SingleIssueCheck if (mID.empty())

  // Behavior common to all instruction classes (can be overridden on a
  // per-class basis): If an exception occurs, perform its actions (commit) and
  // flush the pipe behind it.
  commit_on_exception = true;

  define (instr_class sfx) {
    instructions = ( "addic.", addic, addi, addis, addme, "addme.", addmeo,
                     addo, or, ori, "andi.", oris,
                     rlwinm, add, cmpi, cmp,
                     mfspr, srw, halt,
                     mtspr );

    machine = normal;

    attrs = sfx_instrs;

    define (operands) {
      Src = sources;
      Trg = targets;
    };

    action = {      
    S_INIT: IssueCheck(muldiv_instrs) {
      change_state(S_ID);
    }
    S_ID:  if (Src.can_read_fp(FwdEX,FwdMM) &&  Trg.can_write() && mEX.has_space() && all_stall.can_allocate()) {
        Src.read(FwdEX,FwdMM);
				Trg.allocate();
				change_state(S_EX);
      }
		// No space-check, so one will be inserted.
		S_EX: {
			exec();
      FwdEX.write(Trg);
			change_state(S_MM);
		}
		S_MM: { 
      // The use of Repeat means that the forwarding path will be updated as
      // long as the instruction is in this stage.
      Repeat: FwdMM.write(Trg);
			change_state(S_WB); 
		}
		S_WB: {
      write_ops();
      change_state(S_INIT);
    }

    ExceptionStates;

    };
  }

  define (instr_class deadlock) {
    instructions = ( crxor );

    machine = normal;

    attrs = sfx_instrs;

    define (operands) {
      Src = sources;
      Trg = targets;
    };

    action = {      
    S_INIT: IssueCheck(muldiv_instrs|branch_instrs) {
        dead_flag.allocate();
        change_state(S_ID);
    }
    S_ID:  if (Src.can_read_fp(FwdEX,FwdMM) &&  Trg.can_write() && mEX.has_space() && all_stall.can_allocate() && dead_flag.can_allocate()) {
        Src.read(FwdEX,FwdMM);
				Trg.allocate();
				change_state(S_EX);
      }
		// No space-check, so one will be inserted.
		S_EX: {
			exec();
      FwdEX.write(Trg);
			change_state(S_MM);
		}
		S_MM: { 
      // The use of Repeat means that the forwarding path will be updated as
      // long as the instruction is in this stage.
      Repeat: FwdMM.write(Trg);
			change_state(S_WB); 
		}
		S_WB: {
      write_ops();
      change_state(S_INIT);
    }

    ExceptionStates;

    };
  }

  define (instr_class nop) {
    instructions = ( ori(RA(0),RS(0),UI(0)) );

    allow_missing_operands = true;

    action = {
		S_INIT: IssueCheck(0) { 
			change_state(S_ID); 
		};
    S_ID:  {
				change_state(S_EX);
      }
		S_EX: {
			exec();
			change_state(S_MM);
		}
		S_MM: { 
			change_state(S_WB); 
		}
		S_WB: {
      change_state(S_INIT);
    }

    };
  }

  define (instr_class muldiv) {
    instructions = ( mulli, mullw );

    machine = normal;

    attrs = muldiv_instrs;

    define (operands) {
      Src = sources;
      Trg = targets;
    };

    action = {
		S_INIT: IssueCheck(muldiv_instrs|branch_instrs) { 
			change_state(S_ID); 
		};
    S_ID:  if (Src.can_read_fp(FwdEX,FwdMM) && Trg.can_write() && mEX.has_space() && all_stall.can_allocate()) {
        Src.read(FwdEX,FwdMM);
				Trg.allocate();
				change_state(S_EX);
      }
		// No space-check, so one will be inserted.
		S_EX: {
			exec();
      set_latency(4);
			change_state(S_MM);
		}
		S_MM: if (check_latency() && mWB.has_space()) { 
      Repeat: FwdMM.write(Trg);
        change_state(S_WB); 
      } else {
        decr_latency();
      }
		// No need for a space-check, since S_WBp maps to the same stage.
		S_WB: {
      write_ops();
      change_state(S_INIT);
    }

    ExceptionStates;

    };
  }

#   define HID0_INDEX 50
#   define HID1_INDEX 51

  // A mtspr to HID0 or HID1 allocates the hid_stall semaphore in order to
  // to prevent loads and stores from executing.  Note:  It doesn't check
  // the flag itself.
  define (instr_class mtspr_hid) {
    instructions = (mtspr(SPRN(HID0_INDEX)),mtspr(SPRN(HID1_INDEX)));
    machine = normal;

    define (operands) {
      Src = sources;
      Trg = targets;
    };

    attrs = special_instrs;

#   define HID0_INDEX 50
#   define HID1_INDEX 51

    action = {
    S_INIT: SingleIssueCheck {
      change_state(S_ID);
    }
    S_ID: {
      if (Src.can_read_fp(FwdEX,FwdMM) && Trg.can_write() && mEX.has_space()) {
        Src.read(FwdEX,FwdMM);
        Trg.allocate();
        hid_stall.allocate();
        change_state(S_EX);
      }
    }
    S_EX: if (Mem.can_force_lock()) {
        Mem.allocate_force_lock();
        Fetcher.set_cancel(true);
        Fetcher.cancel();
        exec();
        set_latency(capacity());
        change_state(S_EXp);
      }
    S_EXp: if (check_latency() && mMM.has_space(capacity())) {
        change_state(S_MM);
      } else {
        decr_latency();
      }
    S_MM: {
      change_state(S_WB);
    }
    S_WB: {
      write_ops();
      change_state(S_WBp);
    }
    S_WBp: {
      Mem.deallocate_force_lock();
      hid_stall.deallocate(capacity());
      change_state(S_INIT);
    }

    S_IDe: { change_state(S_EXe); };
    S_EXe: { change_state(S_MMe); };
    S_MMe: { change_state(S_WBe); };
    S_WBe: { change_state(S_INIT); };

    };
  }

  // For testing purposes, we're making this a serially dispatched instruction.
  // It allocates all stages so that nothing can execute until it's done and it
  // can't execute until everything else is done.
  define (instr_class mtmsr) {
    instructions = (mtmsr);
    machine = normal;

    define (operands) {
      Src = sources;
      Trg = targets;
    };

    attrs = special_instrs;

    action = {
    S_INIT: SingleIssueCheck {
      change_state(S_ID);
    }
    S_ID: {
      if (Src.can_read_fp(FwdEX,FwdMM) && all_stall.can_allocate() && hid_stall.can_allocate() 
          && mEX.empty() && mMM.empty() && mWB.empty()) {
        Src.read(FwdEX,FwdMM);
        Trg.allocate();
        all_stall.allocate();
        hid_stall.allocate();
        change_state(S_EX);
      }
    }
    S_EX: {
      exec();
      change_state(S_MM);
    }
    S_MM: {
      change_state(S_WB);
    }
    S_WB: {
      write_ops();
      change_state(S_WBp);
    }
    S_WBp: {
      all_stall.deallocate();
      hid_stall.deallocate(capacity());
      change_state(S_INIT);
    }

    S_IDe: { change_state(S_EXe); };
    S_EXe: { change_state(S_MMe); };
    S_MMe: { change_state(S_WBe); };
    S_WBe: { change_state(S_INIT); };

    };
  }

 define (instr_class load) {
    instructions = ( lbz, lhz, lwz, lwzu, lwzx );

    attrs = load_instrs;

    machine = normal;
    
    define (operands) {
      Src = sources;
      Trg = targets;
      TrgEA = (GPR(RA),target);
    };

    action = {
		S_INIT: LoadStoreIssueCheck(special_instrs|store_instrs|load_instrs|muldiv_instrs|branch_instrs) { 
        change_state(S_ID); 
		};
		S_ID: if (Src.can_read_fp(FwdEX,FwdMM) && Trg.can_write() && 
              mEX.has_space() && hid_stall.can_allocate() && !stalled()) {
        Src.read(FwdEX,FwdMM);
				Trg.allocate();
				change_state(S_EX);
			}
    S_EX: if (mMM.has_space() && Mem.can_request_read() && !stalled()) {
        // Calc ea.
        exec();
        FwdEX.write(TrgEA);
        Mem.send_read_request();
        change_state(S_MM);
      }
		S_MM: if (Mem.can_read()) { 
        exec_and_read(Mem);
        FwdMM.write(Trg);
        change_state(S_MMp);
      }
    // "edge" action:  If we're done executing, then move to writeback, else stay at MM.
    S_MMp: if (exec_done() && mWB.has_space(capacity())) {
        change_state(S_WB);
      }
      else if (Mem.can_request_read()) {
        Mem.send_read_request();
        change_state(S_MM);
      }
		// No need for a space-check, since S_WBp maps to the same stage.
		S_WB: {
      write_ops();
      change_state(S_WBp);
    }
		S_WBp:{
			change_state(S_INIT);
		}

    S_IDe: { change_state(S_EXe); };
    S_EXe: { change_state(S_MMe); };
    S_MMe: { change_state(S_WBe); };
    S_WBe: { change_state(S_INIT); };

    };
  }

  // Note that lmw does 64-bit reads, loading two operands at a time.
  define (instr_class lmw) {
    instructions = (lmw );

    machine = normal;

    define (operands) {
      Src = sources;
      Trg = targets;
    };

    action = {
		S_INIT: LoadStoreIssueCheck(special_instrs|store_instrs|load_instrs|muldiv_instrs|branch_instrs) { 
			change_state(S_ID); 
		};
		S_ID: if (Src.can_read_fp(FwdEX,FwdMM) && Trg.can_write() && all_stall.can_allocate() && !stalled()) {
        Src.read(FwdEX,FwdMM);
				Trg.allocate();
				change_state(S_EX);
			}
    S_EX: if (mMM.has_space() && Mem.can_request_read() && !stalled()) {
        // Calc ea.
        exec();
        Mem.send_read_request(8);
        next_req_mem();
        change_state(S_MM);
      }
		S_MM: if (Mem.can_read()) { 
        exec_and_read(Mem,2);
        change_state(S_MMp);
      }
    S_MMp: if (exec_done() && mWB.has_space()) {
        change_state(S_WB);
      }
      else if (Mem.can_request_read()) {
        Mem.send_read_request(8);
        next_req_mem();
        change_state(S_MM);
      }
		S_WB: {
      write_ops();
      change_state(S_INIT);
    }

    S_IDe: { change_state(S_EXe); };
    S_EXe: { change_state(S_WBe); };
    S_WBe: { change_state(S_INIT); };

    };
  }


 define (instr_class store) {
   instructions = (stb, sth, sthx, stw, stwu, stwx);

    attrs = store_instrs;

    machine = normal;
    
    define (operands) {
      Src = sources;
      TrgEA = (GPR(RA),target);
    };

    action = {
      // Alternative way to check instruction classes by using an attribute query.
		S_INIT: LoadStoreIssueCheck(special_instrs|store_instrs|load_instrs|muldiv_instrs|branch_instrs) { 
        change_state(S_ID); 
		};
		S_ID: if (Src.can_read_fp(FwdEX,FwdMM) && TrgEA.can_write() && 
              mEX.has_space() && hid_stall.can_allocate() && !stalled()) {
				Src.read(FwdEX,FwdMM);
        TrgEA.allocate();
				change_state(S_EX);
			}
    S_EX: if (mMM.has_space(capacity()) && Mem.can_request_write() && !stalled()) {
        // Calc ea.
        exec();
        FwdEX.write(TrgEA);
        Mem.send_write_request();
        change_state(S_MM);
      }
		S_MM: if (Mem.can_write()) { 
        exec_and_write(Mem);
        change_state(S_MMp);
      }
    // "edge" action:  If we're done executing, then move to writeback, else stay at MM.
    S_MMp: if (exec_done() && mWB.has_space(capacity())) {
        change_state(S_WB);
      }
      else if (Mem.can_request_write()) {
        Mem.send_write_request();
        change_state(S_MM);
      }
		// No need for a space-check, since S_WBp maps to the same stage.
		S_WB: {
      write_ops();
      change_state(S_WBp);
    }
		S_WBp: {
      change_state(S_INIT);
    }

    S_IDe: { change_state(S_EXe); };
    S_EXe: { change_state(S_MMe); };
    S_MMe: { change_state(S_WBe); };
    S_WBe: { change_state(S_INIT); };

    };
  }

  // Note that stmw does 64-bit writes, writing two operands at a time.
  define (instr_class stmw) {
    instructions = (stmw );

    machine = normal;

    // For this test, we're treating these as partial accesses.  We're also
    // adding on a false dependency on the CR register to make sure that we can
    // do this.
    define (operands) {
      Src   = sources;
      Src   = (GPR,source,0xffffffff);
      SrcCR = (CR,force_source,0xffffffff);
      Trg   = targets;
    };

    action = {
		S_INIT: LoadStoreIssueCheck(special_instrs|store_instrs|load_instrs|muldiv_instrs|branch_instrs) { 
			change_state(S_ID); 
		};
		S_ID: if (Src.can_read_fp(FwdEX,FwdMM) && Trg.can_write() && SrcCR.can_read_fp(FwdEX,FwdMM) && 
              mEX.has_space() && all_stall.can_allocate() && !stalled()) {
				Src.read(FwdEX,FwdMM);
        // Note: We don't need to read SrcCR b/c it's a false dependency.
        // However, I put it in here just to make sure that no crashes occurred.
        SrcCR.read(FwdEX,FwdMM);
        Trg.allocate();
				change_state(S_EX);
			}
    S_EX: if (mMM.has_space() && Mem.can_request_write() && !stalled()) {
        // Calc ea.
        exec();
        Mem.send_write_request(8);
        next_req_mem();
        change_state(S_MM);
      }
		S_MM: if (Mem.can_write()) { 
        // Do 64-bit writes, unless our request is odd in size, in which case
        // we'll have a 4-byte write at the end.
        exec_and_write(Mem,2,4);
        change_state(S_MMp);
      }
    // "edge" action:  If we're done executing, then move to writeback, else stay at MM.
    S_MMp: if (exec_done() && mWB.has_space()) {
        change_state(S_WB);
      }
      else if (Mem.can_request_write()) {
        Mem.send_write_request(8);
        next_req_mem();
        change_state(S_MM);
      }
		// No need for a space-check, since S_WBp maps to the same stage.
		S_WB: {
      write_ops();
      change_state(S_INIT);
    }

    S_IDe: { change_state(S_EXe); };
    S_EXe: { change_state(S_WBe); };
    S_WBe: { change_state(S_INIT); };

    };
  };

  // This hook is redundant, as the same logic is automatically inserted.
  // However, I just wanted to make sure that this hook would be properly
  // generated.
  branch_hook = func (InstrType &instr) {
    set_speculative(instr,(instr.branch_taken(thisCore()) != BP.last_predict_taken(thisCore(),instr)));
  };

 define (instr_class branch) {
   instructions = ( bc, b, bl, bclr );
   
   machine = normal;

   attrs = branch_instrs;

   define (operands) {
     Src = sources;
     Trg = targets;
   }
   
   action = {
   S_INIT : IssueCheck(branch_instrs|load_instrs|store_instrs|special_instrs) {
       change_state(S_ID);
   };
   S_ID: if (Src.can_read_fp(FwdEX,FwdMM) && Trg.can_write() && mEX.has_space()) {
       Src.read(FwdEX,FwdMM);
       allocate_ops();
       exec();
       write_ops();
       if ( !last_branch_prediction_correct() ) {
         taken_flush();
       }
       BP.update();
       change_state(S_EX);
     }
   S_EX: if (mMM.has_space(capacity())) {
       change_state(S_MM);
     }
   S_MM: if (mWB.has_space(capacity())) {
       change_state(S_WB);
     }
   S_WB: {
     change_state(S_INIT);
   }

    S_IDe: { change_state(S_EXe); };
    S_EXe: { change_state(S_MMe); };
    S_MMe: { change_state(S_WBe); };
    S_WBe: { change_state(S_INIT); };

   };
 }

 define (instr_class decode_miss) {
   instructions = ( decode_miss );
   
   machine = normal;

    define (operands) {
      Src = sources;
      Trg = targets;
    }

   action = {
   S_INIT: {
     change_state(S_ID);
   }
   S_ID: if (can_read_ops() && can_write_ops() && mEX.has_space()) {
       read_ops();
       exec();
       change_state(S_EXe); // We won't get here b/c we'll get a program
                            // exception.
     }
   S_IDe: {
     change_state(S_EXe);
   }
   S_EXe: {
     change_state(S_MMe);
   }
   S_MMe: {
     change_state(S_WBe);
   }
   S_WBe: {
     change_state(S_INIT);
   }

   };
 }

 define (instr_class fetch_exception) {
   instructions = ( fetch_exception );
   
   machine = normal;

   action = {
   S_INIT: {
     change_state(S_ID);
   }
   S_ID: {
     change_state(S_EX);
   }
   S_EX: {
     change_state(S_MM);
   }
   S_MM: {
     change_state(S_WB);
   }
   S_WB: {
     change_state(S_INIT);
   }

   };
 }

};

